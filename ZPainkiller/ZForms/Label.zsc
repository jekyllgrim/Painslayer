class PKCLabel : PKCElement {
	Font fnt;
	string text;
	double textScale;
	AlignType alignment;
	int textColor;
	bool wrap;
	bool autoSize;
	double lineSpacing;

	void config(string text = "", Font fnt = NULL, AlignType alignment = AlignType_TopLeft, bool wrap = true,
				bool autoSize = false, double textScale = 1, int textColor = Font.CR_WHITE, double lineSpacing = 0) {
		if (fnt == NULL) {
			self.fnt = smallfont;
		}
		else {
			self.fnt = fnt;
		}
		self.text = text;
		self.alignment = alignment;
		self.wrap = wrap;
		self.autoSize = autoSize;
		self.textScale = textScale;
		self.textColor = textColor;
		self.alpha = 1;
		self.lineSpacing = lineSpacing;
	}

	PKCLabel init(Vector2 pos, Vector2 size, string text = "", Font fnt = NULL, AlignType alignment = AlignType_TopLeft,
				  bool wrap = true, bool autoSize = false, double textScale = 1, int textColor = Font.CR_WHITE, double lineSpacing = 0) {
		self.config(text, fnt, alignment, wrap, autoSize, textScale, textColor, lineSpacing);
		self.setBox(pos, size);

		return self;
	}

	override void ticker() {
		if (autoSize) {
			if (wrap) {
				int fntHeight = fnt.getHeight();
				BrokenLines textLines = fnt.breakLines(text, int(box.size.x / textScale));
				box.size.y = fntHeight * textScale * textLines.count();
			}
			else {
				int fntHeight = fnt.getHeight();
				box.size.y = fntHeight * textScale;
				box.size.x = fnt.stringWidth(text) * textScale;
			}
		}
	}

	override void drawer() {
		PKCAABB beforeClip = getClipAABB();
		PKCAABB clipTest = boxToScreen();
		PKCAABB clipRect = clipTest.rectOfIntersection(beforeClip);
		PKCAABB screenClip = new("PKCAABB");
		screenClip.size = screenSize();
		clipRect = clipRect.rectOfIntersection(screenClip);
		Screen.setClipRect(int(clipRect.pos.x), int(clipRect.pos.y), int(clipRect.size.x), int(clipRect.size.y));

		if (!wrap) {
			Vector2 pos = getAlignedDrawPos(box.size, (fnt.stringWidth(text), fnt.getHeight()) * textScale, alignment);
			drawText(pos, fnt, text, textColor, textScale);
		}
		else {
			int fntHeight = fnt.getHeight();
			float lineHeight = fntHeight + lineSpacing;

			BrokenLines printLines = fnt.breakLines(text, int(box.size.x / textScale));
			Vector2 pos = getAlignedDrawPos(box.size, (0, lineHeight * printLines.count() * textScale), alignment);

			for (int i = 0; i < printLines.count(); i++) {
				string line = printLines.stringAt(i);
				Vector2 linePos = getAlignedDrawPos(box.size, (fnt.stringWidth(line) * textScale, 0), alignment);
				drawText((linePos.x, pos.y + (lineHeight * i * textScale)), fnt, line, textColor, textScale);
			}
		}

		Screen.setClipRect(int(beforeClip.pos.x), int(beforeClip.pos.y), int(beforeClip.size.x), int(beforeClip.size.y));
	}

	override void onUIEvent(PKCUiEvent ev) {
		if (ev.type == UIEvent.Type_MouseMove) {
			doHover((ev.mouseX, ev.mouseY));
		}
	}
}